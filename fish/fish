#!/usr/bin/env fish

function fish_greeting
    pretty-fortune
    upgradable-report

    if test $fish_private_mode
        echo -e (set_color purple) "\n[Private Mode]\n"
    end
end

function __edit_input
    if test (commandline) = ""
        echo "$history[1]" > /tmp/input
    else
        commandline > /tmp/input
    end
    vim /tmp/input
    commandline -r (cat /tmp/input)
    commandline -f execute
end

# Prefix the previous line with $argv
function __run_with_prefix
    if test (commandline) = ""
        commandline -a $history[1]
    end
    commandline --cursor 0
    commandline -i $argv
    commandline -i ' '
    commandline -f execute
end

function clear_and_reset
    echo -e "\e[?25h" # show cursor in case it was hidden
    /usr/bin/clear
    fish_prompt
end

function fish_user_key_bindings
    bind \es "__run_with_prefix sudo"
    bind \ee "__edit_input"
    bind \cl "clear_and_reset"
    bind \el "clear_and_reset"
end

fish_user_key_bindings

function __fish_cancel_commandline
  commandline ""
end
